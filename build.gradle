plugins {
    id 'java-library'
    id 'maven-publish'
}

configurations {
    doc {
        transitive false
    }
}

group 'ru.abstractmenus'
version '1.14'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/central' }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    implementation 'com.github.AbstractMenus:hocon:1.0.4'
    doc 'com.github.AbstractMenus:hocon:1.0.4:sources'

    compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
}

javadoc {
    title "AbstractMenus ${project.version} API"
    source configurations.doc.collect { zipTree(it) }
    include '**/*.java'
    options.addStringOption('Xdoclint:none', '-quiet')
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}